plugins {
    id 'com.android.application'
}

def locales = file('src/main/res').listFiles(new FilenameFilter() {
    @Override
    boolean accept(File dir, String name) {
        return name.matches('values-.*') && new File(dir, name).list().contains('strings.xml')
    }
}).collect { it.name.substring(7) }

print("Locales: " + locales.join(', '))

android {
    namespace 'com.david42069.dualboothelper'
    compileSdk 34

    defaultConfig {
        applicationId "com.david42069.dualboothelper"
        minSdk 26
        targetSdk 33
        versionCode 4
        versionName "3.1.0"
        buildConfigField "String", "ONEUI_DESIGN_VERSION", "\"${versionName}\""
        resConfigs locales
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    signingConfigs {
        release {
            storeFile file("../release_key.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASSWORD")

            enableV1Signing = true
            enableV2Signing = true
            enableV3Signing = true
            enableV4Signing = true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.implementation {
    exclude (group:"androidx.core",  module:"core")
    exclude (group:"androidx.core",  module:"core-ktx")
    exclude (group:"androidx.customview",  module:"customview")
    exclude (group:"androidx.coordinatorlayout",  module:"coordinatorlayout")
    exclude (group:"androidx.drawerlayout",  module:"drawerlayout")
    exclude (group:"androidx.viewpager2",  module:"viewpager2")
    exclude (group:"androidx.viewpager",  module:"viewpager")
    exclude (group:"androidx.appcompat", module:"appcompat")
    exclude (group:"androidx.fragment", module:"fragment")
    exclude (group:"androidx.preference",  module:"preference")
    exclude (group:"androidx.recyclerview", module:"recyclerview")
    exclude (group:"androidx.slidingpanelayout",  module:"slidingpanelayout")
    exclude (group:"androidx.swiperefreshlayout",  module:"swiperefreshlayout")
    exclude (group:"com.google.android.material", module: "material")
}
dependencies {
    //sesl androidx
    implementation "io.github.oneuiproject:icons:1.1.0"
    implementation "sesl.androidx.core:core:1.15.0+1.0.11-sesl6+rev0"
    implementation "sesl.androidx.core:core-ktx:1.15.0+1.0.0-sesl6+rev0"
    implementation "sesl.androidx.customview:customview:1.2.0-alpha02+1.0.1-sesl6+rev0"
    implementation "sesl.androidx.drawerlayout:drawerlayout:1.2.0+1.0.1-sesl6+rev3"
    implementation "sesl.androidx.viewpager:viewpager:1.1.0-rc01+1.0.0-sesl6+rev0"
    implementation "sesl.androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01+1.0.0-sesl6+rev0"
    implementation "sesl.androidx.coordinatorlayout:coordinatorlayout:1.3.0-alpha02+1.0.0-sesl6+rev0"
    implementation "sesl.androidx.recyclerview:recyclerview:1.4.0-rc01+1.0.21-sesl6+rev0"
    implementation "sesl.androidx.fragment:fragment:1.8.5+1.0.0-sesl6+rev0"
    implementation "sesl.androidx.viewpager2:viewpager2:1.1.0+1.0.0-sesl6+rev0"
    implementation "sesl.androidx.slidingpanelayout:slidingpanelayout:1.2.0+1.0.2-sesl6+rev5"
    implementation "sesl.androidx.appcompat:appcompat:1.7.0+1.0.34-sesl6+rev8"
    implementation "sesl.androidx.preference:preference:1.2.1+1.0.4-sesl6+rev3"
    //sesl samsung
    implementation "sesl.androidx.indexscroll:indexscroll:1.0.3+1.0.3-sesl6+rev4"
    implementation "sesl.androidx.picker:picker-basic:1.0.17+1.0.17-sesl6+rev2"
    implementation "sesl.androidx.picker:picker-color:1.0.6+1.0.6-sesl6+rev3"
    implementation "sesl.androidx.apppickerview:apppickerview:1.0.1+1.0.1-sesl6+rev3"
    //sesl material
    implementation "sesl.com.google.android.material:material:1.12.0+1.0.18-sesl6+rev0"
    implementation 'io.github.tribalfs:oneui-design:0.4.3+oneui6'
    implementation "com.airbnb.android:lottie:6.5.2"
    implementation "androidx.datastore:datastore-preferences-android:1.1.2"
}

dependencies {
    def libsuVersion = '6.0.0'

    // The core module that provides APIs to a shell
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"

    // Optional: APIs for creating root services. Depends on ":core"
    implementation "com.github.topjohnwu.libsu:service:${libsuVersion}"

    // Optional: Provides remote file system support
    implementation "com.github.topjohnwu.libsu:nio:${libsuVersion}"
}

dependencies {


    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
